import org.gradle.api.tasks.testing.logging.TestLogEvent

plugins {
    id 'org.jetbrains.kotlin.jvm'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'java-library'
    id 'org.jlleitschuh.gradle.ktlint'
    id 'com.github.johnrengelman.shadow'
    id 'application'
}

group 'dev.skye.daisy'
def projectTitle = 'daisy-demo'
project.setProperty('archivesBaseName', projectTitle)
mainClassName = 'dev.skye.daisy.Demo'

def environmentVersion = System.getenv('VERSION')
if (environmentVersion == null || environmentVersion.isBlank()) {
    version = '0.0.1-SNAPSHOT'
} else {
    version = environmentVersion.replaceFirst("v", "")
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(":core")
    implementation project(":ktor")

    // Kotlin
    implementation platform('org.jetbrains.kotlin:kotlin-bom')
    implementation platform('org.jetbrains.kotlinx:kotlinx-coroutines-bom:1.5.0')
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-jdk8'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.1.0'

    // Metrics
    implementation 'io.micrometer:micrometer-core:1.6.5'
    implementation "io.micrometer:micrometer-registry-prometheus:1.6.6"

    // Ktor
    implementation platform('io.ktor:ktor-bom:1.5.2')
    implementation 'io.ktor:ktor-server-cio'
    implementation 'io.ktor:ktor-metrics-micrometer'

    // Amazon
    implementation platform('software.amazon.awssdk:bom:2.16.8')
    implementation 'software.amazon.awssdk:sqs'
    implementation 'software.amazon.awssdk:sns'

    // Local
    runtimeOnly 'org.slf4j:slf4j-simple:1.7.9'

    // Tests
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit'
}


compileJava {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

compileKotlin {
    kotlinOptions.useIR = true
    kotlinOptions.jvmTarget = JavaVersion.VERSION_11
}

test {
    testLogging {
        events = [
                TestLogEvent.SKIPPED,
                TestLogEvent.PASSED,
                TestLogEvent.FAILED,
                TestLogEvent.STANDARD_ERROR
        ]
        outputs.upToDateWhen { false }
        showStandardStreams = true
    }
}

shadowJar {
    archivesBaseName = "daisy-demo"
    archiveClassifier = "all"
    mergeServiceFiles()
    exclude "META-INF/*.DSA"
    exclude "META-INF/*.RSA"
}